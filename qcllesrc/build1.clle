/*---------------------------------------------------------------------*/
/* BUILD1 - create all ported code in local library                    */
/*                                                                     */
/* BUILD must be manually created. Once created it can be run and will */
/* build all remaining components in the library passed in.            */
/*                                                                     */
/* A prerequisite to calling build BUILD is to Git CLONE the source    */
/* code from GitHub https://github.com/smartrocks/IBMi                 */
/*                                                                     */ 
/* Parameters                                                          */
/* &lib      Target library for created objects                        */
/* &ifsPath  Path to IFS folder which contains all source files        */
/*           ex: '/home/dave/smartrocks/ibmi'                          */
/*           where the ibmi folder contains qddssrc, qclsrc, etc.      */
/*---------------------------------------------------------------------*/

pgm (&lib &ifsPath) 

dcl &lib *char (10)       /* target library    */
dcl &ifsPath *char (256)  /* source IFS folder */  
dcl &ifsFile *char (256)  /* ifs file path and file name */
dcl &mbrPath *char (256)

chgvar &ifsPath %trim(&ifspath) 

callsubr CrtSrcFls   /* create source physical files */
callsubr CpyIFSsrc   /* copy IFS utility source to source PF's */ 
callsubr ChgSrcTyp   /* change source types in source PF's */
callsubr CompileIFS  /* compile the IFS utility elements */



/*-------------------------------------------------------------*/
subr CrtSrcFls
/***************************************************/
/* create source files in target library as needed */
/***************************************************/
chkobj &lib/QDDSSRC *file
monmsg (CPF9801) exec(CRTSRCPF FILE(&LIB/QDDSSRC) RCDLEN(92))

chkobj &lib/QCLSRC *file
monmsg (CPF9801) exec(CRTSRCPF FILE(&LIB/QCLSRC) RCDLEN(92))

chkobj &lib/QCLLESRC *file
monmsg (CPF9801) exec(CRTSRCPF FILE(&LIB/QCLLESRC) RCDLEN(92))

chkobj &lib/QRPGLESRC *file
monmsg (CPF9801) exec(CRTSRCPF FILE(&LIB/QRPGLESRC) RCDLEN(112))

chkobj &lib/QSQLSRC *file
monmsg (CPF9801) exec(CRTSRCPF FILE(&LIB/QSQLSRC) RCDLEN(92))

chkobj &lib/QSRVSRC *file
monmsg (CPF9801) exec(CRTSRCPF FILE(&LIB/QSRVSRC) RCDLEN(112))

chkobj &lib/QBNDSRC *file
monmsg (CPF9801) exec(CRTSRCPF FILE(&LIB/QBNDSRC) RCDLEN(92))

endsubr
/*-------------------------------------------------------------*/




/*-------------------------------------------------*/
/* copy source needed to build the IFSLIST utility */
/*-------------------------------------------------*/
subr CpyIFSsrc

chgvar &ifsFile (&ifsPath *tcat '/QDDSSRC/IFSLIST.PF') 
chgvar &mbrPath ('/QSYS.LIB/' *tcat &lib +
                  *tcat '.LIB/QDDSSRC.FILE/IFSLIST.MBR')
CPYFRMSTMF FROMSTMF(&ifsFile) +
TOMBR(&mbrPath) MBROPT(*REPLACE) STMFCCSID(*STMF) DBFCCSID(*FILE)                                                                  

chgvar &ifsFile (&ifsPath *tcat '/QCLLESRC/IFSLISTCL.CLLE')
chgvar &mbrPath ('/QSYS.LIB/' *tcat &lib +
                 *tcat '.LIB/QCLLESRC.FILE/IFSLISTCL.MBR')
CPYFRMSTMF FROMSTMF(&ifsFile) +
TOMBR(&mbrPath) MBROPT(*REPLACE)

chgvar &ifsFile (&ifsPath *tcat '/QRPGLESRC/IFSLIST.RPGLE')
chgvar &mbrPath ('/QSYS.LIB/' *tcat &lib +
                *tcat '.LIB/QRPGLESRC.FILE/IFSLIST.MBR')
CPYFRMSTMF FROMSTMF(&ifsFile) +
TOMBR(&mbrPath) MBROPT(*REPLACE)

chgvar &ifsFile (&ifsPath *tcat '/QCMDSRC/IFSLIST.CMD')
chgvar &mbrPath ('/QSYS.LIB/' *tcat &lib +
                 *tcat '.LIB/QCMDSRC.FILE/IFSLIST.MBR')
CPYFRMSTMF FROMSTMF(&ifsFile) +
TOMBR(&mbrPath) MBROPT(*REPLACE)

chgvar &ifsFile (&ifsPath *tcat '/QRPGLESRC/STDTYPH.RPGCPY')
chgvar &mbrPath ('/QSYS.LIB/' *tcat &lib +
                 *tcat '.LIB/QRPGLESRC.FILE/STDTYPH.MBR')
CPYFRMSTMF FROMSTMF(&ifsFile) +
TOMBR(&mbrPath) MBROPT(*REPLACE)

chgvar &ifsFile (&ifsPath *tcat '/QRPGLESRC/STMIOH.RPGCPY')
chgvar &mbrPath ('/QSYS.LIB/' *tcat &lib +
                 *tcat '.LIB/QRPGLESRC.FILE/STMIOH.MBR')
CPYFRMSTMF FROMSTMF(&ifsFile) +
TOMBR(&mbrPath) MBROPT(*REPLACE)

endsubr




/**************************************/
/* add source types to source members */
/**************************************/
subr ChgSrcTyp
CHGPFM FILE(&LIB/QDDSSRC) MBR(IFSLIST) SRCTYPE(PF) TEXT('IFS List Utility')
CHGPFM FILE(&LIB/QCLLESRC) MBR(IFSLISTCL) SRCTYPE(CLLE) TEXT('IFS List Utility')
CHGPFM FILE(&LIB/QRPGLESRC) MBR(IFSLIST) SRCTYPE(RPGLE) TEXT('IFS List Utility')
CHGPFM FILE(&LIB/QCMDSRC) MBR(IFSLIST) SRCTYPE(CMD) TEXT('IFS List Utility')
endsubr



/****************************************/
/* Compile the IFSLIST command elements */
/****************************************/
Subr CompileIFS

DLTF &LIB/IFSLIST
monmsg CPF2105 /* file not found */
CRTPF FILE(&LIB/IFSLIST) SRCFILE(&LIB/QDDSSRC) LVLCHK(*NO)

CRTBNDCL PGM(&LIB/IFSLISTCL) SRCFILE(&LIB/QCLLESRC)
CRTBNDRPG PGM(&LIB/IFSLIST) SRCFILE(&LIB/QRPGLESRC)    
CRTCMD CMD(&LIB/IFSLIST) PGM(&LIB/IFSLISTCL) SRCFILE(&LIB/QCMDSRC)

endsubr

endpgm